<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
         xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/halo"
		 creationComplete="onCreationComplete();">
	<fx:Script>
		<![CDATA[
			import com.mailbrew.components.skins.AccountListSkin;
			import com.mailbrew.data.AccountInfo;
			import com.mailbrew.data.AccountTypes;
			import com.mailbrew.data.MainAppViews;
			import com.mailbrew.database.Database;
			import com.mailbrew.database.DatabaseEvent;
			import com.mailbrew.database.DatabaseResponder;
			import com.mailbrew.events.CheckMailEvent;
			import com.mailbrew.events.DeleteAccountEvent;
			import com.mailbrew.events.PopulateAccountInfoEvent;
			import com.mailbrew.model.ModelLocator;
			
			import flash.display.NativeMenu;
			import flash.display.NativeMenuItem;
			import flash.events.Event;
			import flash.events.MouseEvent;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.ListEvent;
			
			import spark.components.Label;
			import spark.events.IndexChangeEvent;
			
			private var ml:ModelLocator;
			
			private function onCreationComplete():void
			{
				this.ml = ModelLocator.getInstance();
			}
			
			private function onRemoveAccount(e:Event):void
			{
				if (this.accountList.selectedItem == null) return;
				Alert.show("Are you sure you want to delete " + this.accountList.selectedItem.label + "?",
						   "Confirm",
						   Alert.YES|Alert.NO,
						   null,
						   onRemoveAccountConfirm,
						   ml.faceSurpriseIconClass);
			}
			
			private function onRemoveAccountConfirm(e:CloseEvent):void
			{
				if (this.accountList.selectedItem == null) return;
				if (e.detail == Alert.YES)
				{
					var dae:DeleteAccountEvent = new DeleteAccountEvent();
					dae.accountId = this.accountList.selectedItem.accountId;
					dae.dispatch();
					this.accountList.selectedItem = null;
				}
			}

			private function onCheckNow(e:Event):void
			{
				if (this.accountList.selectedItem == null) return;
				var accountId:Number = this.accountList.selectedItem.accountId;
				var cme:CheckMailEvent = new CheckMailEvent();
				cme.accountIds = [accountId];
				cme.dispatch();
			}
			
			private function onAccountListChange(e:IndexChangeEvent):void
			{
				var list:List = e.target as List;
				if (list.selectedItem == null) return;
				if (this.ml.mainAppView == MainAppViews.INTRO)
				{
					this.ml.mainAppView = MainAppViews.STARTED;
				}
				var paie:PopulateAccountInfoEvent = new PopulateAccountInfoEvent();
				paie.accountId = list.selectedItem.accountId;
				paie.dispatch();
			}
			
			private function onAddAccount():void
			{
				this.accountList.selectedItem = null;
				this.removeAccountButton.enabled = false;
				if (this.ml.mainAppView == MainAppViews.INTRO)
				{
					this.ml.mainAppView = MainAppViews.STARTED;
				}
				this.ml.accountInfo = null;
			}
			
			public function onRightClick(e:MouseEvent):void
			{
				var data:Object = this.getData(e.target);
				if (data == null) return;
				this.accountList.selectedItem = data;
				var menu:NativeMenu = new NativeMenu();
				var checkItem:NativeMenuItem = new NativeMenuItem("Check Now");
				checkItem.addEventListener(Event.SELECT, onCheckNow);
				checkItem.enabled = (data.active && data.working);
				menu.addItem(checkItem);
				if (!data.working)
				{
					var errorItem:NativeMenuItem = new NativeMenuItem("View Error");
					errorItem.addEventListener(Event.SELECT, showErrorMessage);
					menu.addItem(errorItem);
				}
				var deleteItem:NativeMenuItem = new NativeMenuItem("Delete");
				deleteItem.addEventListener(Event.SELECT, onRemoveAccount);
				menu.addItem(deleteItem);
				menu.display(this.stage, e.stageX, e.stageY);
			}
			
			private function getData(o:Object):Object
			{
				if (o == null) return null;
				if (o is AccountListSkin) return o.data;
				return getData(o.parent);
			}
			
			private function showErrorMessage(e:Event):void
			{
				if (this.accountList.selectedItem == null) return;
				var accountId:Number = this.accountList.selectedItem.accountId;
				var db:Database = this.ml.db;
				var responder:DatabaseResponder = new DatabaseResponder();
				var listener:Function = function(e:DatabaseEvent):void
				{
					responder.removeEventListener(DatabaseEvent.RESULT_EVENT, listener);
					var errorViewer:ErrorViewer = new ErrorViewer();
					errorViewer.errorMessage = e.data;
					errorViewer.open();
				};
				responder.addEventListener(DatabaseEvent.RESULT_EVENT, listener);
				db.getErrorMessage(responder, accountId);
			}
		]]>
	</fx:Script>
	
	<s:layout>
		<s:VerticalLayout horizontalAlign="center"/>
	</s:layout>
	
	<s:List id="accountList"
			width="100%" height="100%"
			dataProvider="{ModelLocator.getInstance().accounts}"
			change="onAccountListChange(event);" rightClick="onRightClick(event);"
			itemRenderer="com.mailbrew.components.skins.AccountListSkin"/>

	<s:Group bottom="0">
		<s:layout>
			<s:HorizontalLayout/>
		</s:layout>
		<mx:Button id="addAccountButton" width="40" icon="{ModelLocator.getInstance().listAddIconClass}" click="onAddAccount();"/>
		<mx:Button id="removeAccountButton" width="40" icon="{ModelLocator.getInstance().listRemoveIconClass}" click="onRemoveAccount(event)" enabled="{(this.accountList.selectedItem == null) ? false : true}"/>
	</s:Group>	
	
</s:Group>
