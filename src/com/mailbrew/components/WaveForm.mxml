<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/halo"
		   xmlns:c="com.mailbrew.components.*"
		   creationComplete="onCreationComplete();">
	<fx:Script>
		<![CDATA[
			import com.mailbrew.data.AccountInfo;
			import com.mailbrew.data.AccountSaveMode;
			import com.mailbrew.data.AccountTypes;
			import com.mailbrew.events.SaveAccountEvent;
			import com.mailbrew.events.VerifyAccountEvent;
			import com.mailbrew.model.ModelLocator;
			
			import mx.binding.utils.ChangeWatcher;
			import mx.events.PropertyChangeEvent;
			private var accountId:Number;
			
			private function onCreationComplete():void
			{
				ChangeWatcher.watch(ModelLocator.getInstance(), "accountInfo", onAccountInfoChange);
				this.onFormChange();
			}
			
			private function onAccountInfoChange(e:PropertyChangeEvent):void
			{
				this.clearData();
				var ml:ModelLocator = ModelLocator.getInstance();
				if (ml.accountInfo == null) return;
				if (ml.accountInfo.accountType == AccountTypes.GOOGLE_WAVE)
				{
					ml.accountFormView = AccountTypes.GOOGLE_WAVE;
					this.setData(ml.accountInfo);
				}
			}
			
			private function onSave():void
			{
				var sae:SaveAccountEvent = new SaveAccountEvent();
				sae.accountType = AccountTypes.GOOGLE_WAVE;
				sae.saveMode = (isNaN(this.accountId)) ? AccountSaveMode.INSERT : AccountSaveMode.UPDATE;
				sae.accountId = this.accountId;
				sae.accountName = this.accountNameInput.text;
				sae.username = this.usernameInput.text;
				sae.password = this.passwordInput.text;
				sae.notificationPosition = this.notificationPositionInput.selectedItem.data;
				sae.notificationSound = this.notificationSound.value;
				sae.active = this.activeCheckbox.selected;
				sae.dispatch();
			}
			
			public function setData(accountInfo:AccountInfo):void
			{
				this.accountId = accountInfo.accountId;
				this.accountNameInput.text = accountInfo.accountName;
				this.usernameInput.text = accountInfo.username;
				this.passwordInput.text = accountInfo.password;
				this.notificationPositionInput.setSelectionByData(accountInfo.notificationPosition);
				this.notificationSound.value = accountInfo.notificationSound;
				this.activeCheckbox.selected = accountInfo.active;
				this.saveButton.label = "Update";
				this.onFormChange();
			}
			
			public function clearData():void
			{
				this.accountId = NaN;
				this.accountNameInput.text = "";
				this.usernameInput.text = "";
				this.passwordInput.text = "";
				this.notificationPositionInput.clearSelection();
				this.notificationSound.clearSelection();
				this.activeCheckbox.selected = true;
				this.saveButton.label = "Save";
			}

			private function isFormComplete():Boolean
			{
				if (usernameInput.text.length > 0 && passwordInput.length > 0 && accountNameInput.text.length > 0) return true;
				return false;
			}
			
			private function onFormChange():void
			{
				var formComplete:Boolean = this.isFormComplete();
				saveButton.enabled = formComplete;
				verifyButton.enabled = formComplete;
			}
			
			private function onVerify():void
			{
				if (!this.isFormComplete()) return;
				var vae:VerifyAccountEvent = new VerifyAccountEvent();
				vae.accountType = AccountTypes.GOOGLE_WAVE;
				vae.username = this.usernameInput.text;
				vae.password = this.passwordInput.text;
				vae.dispatch();
			}
		]]>
	</fx:Script>
	<mx:Text height="46" left="5" right="5" top="5">
		<mx:text>
			<fx:String>Note: Google Wave support is currently experimental. If something breaks, expect an application update very soon.</fx:String>
		</mx:text>
	</mx:Text>
	<mx:Form top="40" bottom="30" right="0" left="0">
		<mx:FormItem label="Account Name">
			<mx:TextInput id="accountNameInput" width="200" change="onFormChange();"/>
		</mx:FormItem>
		<mx:FormItem label="Google Username">
			<mx:TextInput id="usernameInput" width="200" change="onFormChange();"/>
		</mx:FormItem>
		<mx:FormItem label="Google Password">
			<mx:TextInput id="passwordInput" width="200" displayAsPassword="true" change="onFormChange();"/>
		</mx:FormItem>
		<mx:FormItem label="Notification Position">
			<c:PositionComboBox id="notificationPositionInput"/>
		</mx:FormItem>
		<mx:FormItem label="Notification Sound">
			<c:SoundPicker id="notificationSound"/>
		</mx:FormItem>
		<mx:FormItem label="Active?">
			<mx:CheckBox id="activeCheckbox" selected="true"/>
		</mx:FormItem>
	</mx:Form>
	<mx:Button id="saveButton" label="Save" click="onSave();" left="100" bottom="0"/>
	<mx:Button id="verifyButton" label="Verify" click="onVerify();" right="100" bottom="0"/>
</mx:Canvas>
