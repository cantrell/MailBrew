<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
		   xmlns:c="com.mailbrew.components.*"
		   creationComplete="onCreationComplete();">
	<mx:Script>
		<![CDATA[
			import com.mailbrew.data.AccountInfo;
			import com.mailbrew.data.AccountSaveMode;
			import com.mailbrew.data.AccountTypes;
			import com.mailbrew.events.SaveAccountEvent;
			import com.mailbrew.model.ModelLocator;
			
			import mx.binding.utils.ChangeWatcher;
			import mx.events.PropertyChangeEvent;
			private var accountId:Number;
			
			private function onCreationComplete():void
			{
				ChangeWatcher.watch(ModelLocator.getInstance(), "accountInfo", onAccountInfoChange);
			}
			
			private function onAccountInfoChange(e:PropertyChangeEvent):void
			{
				this.clearData();
				var ml:ModelLocator = ModelLocator.getInstance();
				if (ml.accountInfo == null) return;
				if (ml.accountInfo.accountType == AccountTypes.GMAIL)
				{
					ml.accountFormView = AccountTypes.GMAIL;
					this.setData(ml.accountInfo);
				}
			}
			
			private function onSave():void
			{
				var sae:SaveAccountEvent = new SaveAccountEvent();
				sae.accountType = AccountTypes.GMAIL;
				sae.saveMode = (isNaN(this.accountId)) ? AccountSaveMode.INSERT : AccountSaveMode.UPDATE;
				sae.accountId = this.accountId;
				sae.accountName = this.accountNameInput.text;
				sae.username = this.usernameInput.text;
				sae.password = this.passwordInput.text;
				sae.notificationPosition = this.notificationPositionInput.selectedItem.data;
				sae.sound = null; // TBD: implement sounds
				sae.active = this.activeCheckbox.selected;
				sae.dispatch();
			}
			
			public function setData(accountInfo:AccountInfo):void
			{
				this.accountId = accountInfo.accountId;
				this.accountNameInput.text = accountInfo.accountName;
				this.usernameInput.text = accountInfo.username;
				this.passwordInput.text = accountInfo.password;
				this.notificationPositionInput.setSelectionByData(accountInfo.notificationPosition);
				// TBD: implement sounds
				this.activeCheckbox.selected = accountInfo.active;
				this.saveButton.label = "Update";
			}
			
			public function clearData():void
			{
				this.accountId = NaN;
				this.accountNameInput.text = "";
				this.usernameInput.text = "";
				this.passwordInput.text = "";
				this.notificationPositionInput.clearSelection();
				// TBD: implement sounds
				this.activeCheckbox.selected = true;
				this.saveButton.label = "Save";
			}

		]]>
	</mx:Script>
	<mx:Form left="0" right="0" top="0" bottom="30">
		<mx:FormItem label="Account Name">
			<mx:TextInput id="accountNameInput" width="200"/>
		</mx:FormItem>
		<mx:FormItem label="Gmail Username">
			<mx:TextInput id="usernameInput" width="200"/>
		</mx:FormItem>
		<mx:FormItem label="Gmail Password">
			<mx:TextInput id="passwordInput" width="200" displayAsPassword="true"/>
		</mx:FormItem>
		<mx:FormItem label="Notification Position">
			<c:PositionComboBox id="notificationPositionInput"/>
		</mx:FormItem>
		<mx:FormItem label="Active?">
			<mx:CheckBox id="activeCheckbox" selected="true"/>
		</mx:FormItem>
	</mx:Form>
	<mx:Button id="saveButton" label="Save" click="onSave();" left="149" right="141" bottom="0"/>
</mx:Canvas>
